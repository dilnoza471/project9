/**
* FlappyGame 
* FlappyGameObject.run returns score when the player crashes, and 0 (false) when the player quits (q)
*/
class FlappyGame {
	field int delay;
	
	field Flappy player;
	field int player_tlx;
	field int player_tly;
	field int player_brx;
	field int player_bry;
	
	field Pipe pipe1;
	field Pipe pipe2;
	field Pipe pipe3;
	
	field boolean crashed;
	
	
	
	field int clock;
	field int score;
	field int high_score;
	
	constructor FlappyGame new(int highest_score) {
		let player = Flappy.new(4000);
		let player_tlx = 4;
		let player_brx = 5;
		let player_tly = player.collision_box_tly();
		let player_bry = player_tly + 32;
	
		let pipe1 = Pipe.new();
		let pipe2 = Pipe.new();
		let pipe3 = Pipe.new();
		
		let delay = 7;  // start slow?
		let crashed = false;
		
		let clock = 0;
		let score = 0;
		let high_score = highest_score;
		
		// setup screen
		do Screen.clearScreen();
		do Output.moveCursor(0,0);
		do Output.printString("High Score: ");
		do Output.moveCursor(0,12);
		do Output.printInt(high_score);
		
		do Output.moveCursor(1,0);
		do Output.printString("Score: ");
		do Output.moveCursor(1,7);
		do Output.printInt(score);
		// ground
		/*do Screen.setColor(true);
		do Screen.drawLine(0, 244, 511, 244);*/
		
		return this;
	}

	method void dispose() {
		do player.dispose();
		
		do pipe1.dispose();
		do pipe2.dispose();
		do pipe3.dispose();
		
		do Memory.deAlloc(this);
		return;
	}
	
	method boolean check_input() {
		var int key;
		let key = Keyboard.keyPressed();
		
		if (key = 32) { // space bar to jump ( & ~(last_cycle_key = 32) to prevent multiple jumps from one space bar press)
			do player.jump();
		}
		if (key = 131) { // up arrow to jump
			do player.jump();
		}
		if (key = 81) {  // Q to quit
			return false;
		}
		//let last_cycle_key = key;
		return true;
	}

	method int run() {
		while (~crashed) {
			if (check_input()) {
				// update each object's state
				do player.update();
				
				do pipe1.update();
				do pipe2.update();
				do pipe3.update();

				do Pipe.decrement_rightmost();
				
				// check for collisions
				let player_tly = player.collision_box_tly();
				let player_bry = player_tly + 32;
				if (pipe1.collision(player_tlx, player_tly, player_brx, player_bry) | 
					pipe2.collision(player_tlx, player_tly, player_brx, player_bry) |
					pipe3.collision(player_tlx, player_tly, player_brx, player_bry)
					) {
					
					let crashed = true;
					}
				do Sys.wait(delay);  // game speed delay
				
			}
			else {
				return 0; // quit
			}
			if (clock = 3) { // update and display score ever 4 cycles
				let clock = 0; 
				let score = score + 1;
				do Output.moveCursor(1,7);
				do Output.printInt(score);
				
				// update delay if necessary
				if ((score = 500) | (score = 1000) | (score = 1500) | (score = 2000)) {
					let delay = delay - 1;
				}
			}
			else {let clock = clock + 1;}
			
			
			
		}
		// do endScreen
		if (score > high_score) {return score;}
		return high_score;
	}
}